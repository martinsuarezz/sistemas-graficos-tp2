<!doctype html>
<html>
    <head>
        <title>TP1 - Demo Helicoptero</title>
        <meta http-equiv="Cache-control" content="no-cache">        
        <style>
            body{ background-color: grey; }
            canvas{ background-color: rgb(15, 15, 15); }
			textarea{ background-color: black; foreground-color: white;}
        </style>
        <link rel="stylesheet" href="css/main.css">
    </head>
    <body>
        
    	<center> 
            <div class="container">
                <canvas id="my-canvas" width="1400" height="800" tabindex='1'></canvas>
                <div id="overlay">
                  <div>X: <span id="xPos"></span></div>
                  <div>Z: <span id="zPos"></span></div>
                  <div>Altura: <span id="yPos"></span></div>
                  <div>Velocidad: <span id="speed"></span></div>
                </div>
              </div>   		            	
        </center>

        <script type="text/javascript" src="js/gl-matrix.js"></script>
        <script type="text/javascript" src="js/superficieBarrido.js"></script>
        <script type="text/javascript" src="js/objeto3D.js"></script>
        <script type="text/javascript" src="js/cabin.js"></script>
        <script type="text/javascript" src="js/blade.js"></script>
        <script type="text/javascript" src="js/terrain.js"></script>
        <script type="text/javascript" src="js/waterTerrain.js"></script>
        <script type="text/javascript" src="js/cylinder.js"></script>
        <script type="text/javascript" src="js/box.js"></script>
        <script type="text/javascript" src="js/tail.js"></script>
        <script type="text/javascript" src="js/tailBeam.js"></script>
        <script type="text/javascript" src="js/landingPad.js"></script>
        <script type="text/javascript" src="js/skidLowerPart.js"></script>
        <script type="text/javascript" src="js/rotor.js"></script>
        <script type="text/javascript" src="js/sphere.js"></script>
        <script type="text/javascript" src="js/flap.js"></script>
        <script type="text/javascript" src="js/shieldedRotor.js"></script>
        <script type="text/javascript" src="js/rotorShield.js"></script>
        <script type="text/javascript" src="js/arm.js"></script>
        <script type="text/javascript" src="js/skid.js"></script>
        <script type="text/javascript" src="js/camera.js"></script>
        <script type="text/javascript" src="js/cameraController.js"></script>
        <script type="text/javascript" src="js/helicopterController.js"></script>
        <script type="text/javascript" src="js/controller.js"></script>
        <script type="text/javascript" src="js/curvaBezier.js"></script>
        <script type="text/javascript" src="js/recta.js"></script>
        <script type="text/javascript" src="js/forma.js"></script>
        <script type="text/javascript" src="js/helicopter.js"></script>
        <script type="text/javascript" src="js/main.js"></script>
        
        <script id="shader-vs" type="x-shader/x-vertex">

            precision highp float;
            
            attribute vec3 aVertexPosition;
            attribute vec3 aVertexNormal;
            attribute vec2 aUv;
          
            uniform mat4 modelMatrix;            
            uniform mat4 viewMatrix;
            uniform mat4 projMatrix;

            uniform mat4 normalMatrix;

            varying vec3 vNormal;    
            varying vec3 vPosWorld;
            varying vec2 vUv;
            varying vec3 modelPosition;

            void main(void) {
                gl_Position = projMatrix * viewMatrix * modelMatrix * vec4(aVertexPosition, 1.0);

                vPosWorld = (modelMatrix * vec4(aVertexPosition, 1.0)).xyz;    //la posicion en coordenadas de mundo
                vNormal = (normalMatrix * vec4(aVertexNormal, 1.0)).xyz;       //la normal en coordenadas de mundo
                //vNormal = aVertexNormal;       
                vUv = aUv;
                modelPosition = aVertexPosition;
            }
        </script>

        <script id="terrain-vs" type="x-shader/x-vertex" >
            // atributos del vértice (cada uno se alimenta de un ARRAY_BUFFER distinto)
            attribute vec3 aPosition;   //posicion (x,y,z)
            attribute vec3 aNormal;     //vector  normal (x,y,z)
            attribute vec2 aUv;         //coordenadas de texture (x,y)  x e y (en este caso) van de 0 a 1
            
            // variables Uniform (son globales a todos los vértices y de solo-lectura)
            
            uniform mat4 modelMatrix;            
            uniform mat4 viewMatrix;
            uniform mat4 projMatrix;
            uniform mat4 normalMatrix; 
            
            uniform sampler2D uSamplerH;         // sampler de textura del heightMap
            
            uniform vec2 helicopterCoords;
            
            // variables varying (comunican valores entre el vertex-shader y el fragment-shader)
            // Es importante remarcar que no hay una relacion 1 a 1 entre un programa de vertices y uno de fragmentos
            // ya que por ejemplo 1 triangulo puede generar millones de pixeles (dependiendo de su tamaño en pantalla)
            // por cada vertice se genera un valor de salida en cada varying.
            // Luego cada programa de fragmentos recibe un valor interpolado de cada varying en funcion de la distancia
            // del pixel a cada uno de los 3 vértices. Se realiza un promedio ponderado
            
            varying vec3 vPosWorld;
            varying vec3 vNormal;
            varying vec2 vUv;
            varying vec3 vWorldPosition;
            varying float height;

            const float epsilon=0.01;
            const float amplitud=300.0;

            void main(void) {
                        
                vec3 position = aPosition;		
                vec3 normal = aNormal;
                vec2 uv = aUv;
                
                uv = uv / 8.0;
                uv.s = uv.s + helicopterCoords.y / 8.0;
                uv.t = 1.0 - uv.t + helicopterCoords.x / 8.0;
                
                vec4 center = texture2D(uSamplerH, vec2(uv.s, uv.t));                     
                vec4 masU = texture2D(uSamplerH, vec2(uv.s+epsilon, uv.t));  
                vec4 masV = texture2D(uSamplerH, vec2(uv.s, uv.t+epsilon));  

                vec4 menosU = texture2D(uSamplerH, vec2(uv.s-epsilon, uv.t));  
                vec4 menosV = texture2D(uSamplerH, vec2(uv.s, uv.t-epsilon));
                                        
                position.y += center.x * amplitud;
                height = center.x;

                vec4 worldPos = modelMatrix * vec4(position, 1.0);                        

                gl_Position = projMatrix * viewMatrix * worldPos;
            
                vPosWorld = worldPos.xyz;

                float angU = atan((masU.x - center.x) * amplitud, epsilon);
                float angV = atan((masV.x - center.x) * amplitud, epsilon);

                // tangentes en U y en V
                vec3 gradU1= vec3(cos(angU), sin(angU), 0.0);
                vec3 gradV1= vec3(0.0      , sin(angV), cos(angV));
                
                angU=atan((center.x-menosU.x)*amplitud,epsilon);
                angV=atan((center.x-menosV.x)*amplitud,epsilon);

                // segundo conjunto de tangentes en U y en V
                vec3 gradU2=vec3(cos(angU),sin(angU),0.0);
                vec3 gradV2=vec3(0.0      ,sin(angV),cos(angV));

                // calculo el producto vectorial
                vec3 tan1 = (gradV1 + gradV2) / 2.0;
                vec3 tan2 = (gradU1 + gradU2) / 2.0;
                vNormal = cross(tan1,tan2);
                vNormal = normalize(vNormal);
                vUv = uv;
            }
        </script>

        <script id="water-vs" type="x-shader/x-vertex" >
          // atributos del vértice (cada uno se alimenta de un ARRAY_BUFFER distinto)
          attribute vec3 aPosition;   //posicion (x,y,z)
          attribute vec3 aNormal;     //vector  normal (x,y,z)
          attribute vec2 aUv;         //coordenadas de texture (x,y)  x e y (en este caso) van de 0 a 1
          
          // variables Uniform (son globales a todos los vértices y de solo-lectura)
          
          uniform mat4 modelMatrix;            
          uniform mat4 viewMatrix;
          uniform mat4 projMatrix;
          uniform mat4 normalMatrix; 

          uniform float time;
          
          uniform sampler2D uSampler;         // textura del agua
          
          // variables varying (comunican valores entre el vertex-shader y el fragment-shader)
          // Es importante remarcar que no hay una relacion 1 a 1 entre un programa de vertices y uno de fragmentos
          // ya que por ejemplo 1 triangulo puede generar millones de pixeles (dependiendo de su tamaño en pantalla)
          // por cada vertice se genera un valor de salida en cada varying.
          // Luego cada programa de fragmentos recibe un valor interpolado de cada varying en funcion de la distancia
          // del pixel a cada uno de los 3 vértices. Se realiza un promedio ponderado
          
          varying vec3 vPosWorld;
          varying vec3 vNormal;
          varying vec2 vUv;
          varying vec3 vWorldPosition;
          varying float height;

          const float epsilon=0.1;

          const float A1 = 3.0;
          const float kx1 = 0.03;
          const float kz1 = 0.03;
          const float w1 = 1.0;

          const float A2 = 0.2;
          const float kx2 = 2.0;
          const float kz2 = -1.0;
          const float w2 = 0.3;

          void main(void) {
                      
              vec3 position = aPosition;		
              vec3 normal = aNormal;
              vec2 uv = aUv;
                            
              float altura = A1 * sin(kx1 * position.x + kz1 * position.z + w1 * time) + A2 * sin(kx2 * position.x + kz2 * position.z + w2 * time);
              float alturaMasX = A1 * sin(kx1 * (position.x + epsilon) + kz1 * position.z + w1 * time) + A2 * sin(kx2 * (position.x + epsilon) + kz2 * position.z + w2 * time);
              float alturaMasZ = A1 * sin(kx1 * position.x + kz1 * (position.z + epsilon) + w1 * time) + A2 * sin(kx2 * position.x + kz2 * (position.z + epsilon) + w2 * time);
              float alturaMenosX = A1 * sin(kx1 * (position.x + epsilon) + kz1 * position.z + w1 * time) + A2 * sin(kx2 * (position.x + epsilon) + kz2 * position.z + w2 * time);
              float alturaMenosZ = A1 * sin(kx1 * position.x + kz1 * (position.z + epsilon) + w1 * time) + A2 * sin(kx2 * position.x + kz2 * (position.z + epsilon) + w2 * time);
                                      
              position.y += altura;

              vec4 worldPos = modelMatrix * vec4(position, 1.0);                        

              gl_Position = projMatrix * viewMatrix * worldPos;
          
              vPosWorld = worldPos.xyz;

              float angU = atan(altura - alturaMasX, epsilon);
              float angV = atan(altura - alturaMasZ, epsilon);

              // tangentes en U y en V
              vec3 gradU1= vec3(cos(angU), sin(angU), 0.0);
              vec3 gradV1= vec3(0.0      , sin(angV), cos(angV));
              
              angU = atan((altura - alturaMenosX), epsilon);
              angV = atan((altura - alturaMenosZ), epsilon);

              // segundo conjunto de tangentes en U y en V
              vec3 gradU2 = vec3(cos(angU),sin(angU),0.0);
              vec3 gradV2 = vec3(0.0      ,sin(angV),cos(angV));

              // calculo el producto vectorial
              vec3 tan1 = (gradV1 + gradV2) / 2.0;
              vec3 tan2 = (gradU1 + gradU2) / 2.0;
              vNormal = cross(tan1,tan2);
              vNormal = normalize(vNormal);
              vUv = uv;
          }
      </script>

        <script id="shader-fs" type="x-shader/x-fragment">
            precision highp float;

            uniform vec3 objColor;
            varying vec3 vNormal;
            varying vec3 vPosWorld;

            vec3 calculateIlumination(vec3 normal){
              const float ambientReflection = 0.1;
              const vec3 ambientLight = vec3(1.0, 1.0, 1.0);

              const vec3 lightVector = vec3(0, 1.0, 0);
              const float diffuseReflection = 0.1;
              const vec3 diffuseLight = vec3(1.0, 1.0, 1.0);

              const vec3 lightVector2 = normalize(vec3(1.0, 0.2, 0));
              const float diffuseReflection2 = 0.13;
              const vec3 diffuseLight2 = vec3(1.0, 1.0, 1.0);

              return ambientReflection * ambientLight + diffuseReflection * dot(lightVector, normal) * diffuseLight + diffuseReflection2 * dot(lightVector2, normal) * diffuseLight2;
            }

            void main(void) {
                vec3 ilum = calculateIlumination(vNormal);

                vec3 color = mix(objColor, ilum, 0.2);

                /*
                gl_FragColor = vec4(color, 1.0);
                //gl_FragColor = vec4(vNormal, 1.0);
                vec3 lightVec=normalize(vec3(0.0,3.0,5.0)-vPosWorld);
                vec3 diffColor=mix(vec3(0.7,0.7,0.7),vNormal,0.4);
                vec3 color=dot(lightVec,vNormal)*diffColor+vec3(0.2,0.2,0.2);
                color = mix(objColor, color, 0.2);
                */

                gl_FragColor = vec4(color, 1.0);
                //gl_FragColor = vec4(vNormal, 1.0);
            }
        </script>

        <script id="sky-fs" type="x-shader/x-fragment">
          precision highp float;

          varying vec3 vNormal;
          varying vec3 vPosWorld;
          varying vec2 vUv;
          varying vec3 modelPosition;

          uniform sampler2D uSampler; // textura del objeto

          void main(void) {

              vec2 auxUv = vUv;

              vec3 lightVec=normalize(vec3(0.0,3.0,5.0)-vPosWorld);
              vec3 diffColor=mix(vec3(0.7,0.7,0.7),vNormal,0.4);
              vec3 color=dot(lightVec,vNormal)*diffColor+vec3(0.2,0.2,0.2);

              float uvFactorX = 0.0004;
              float uvFactorY = 0.00027;
              float shiftY = 0.3;
              float shiftX = 0.4;

              auxUv.x = modelPosition.x * uvFactorX + shiftX;
              auxUv.y = modelPosition.y * uvFactorY + shiftY;
              
              vec3 texture=texture2D(uSampler, auxUv).xyz;
              color = mix(texture, color, 0.05);
              //color = vec3(vPosWorld.y - 100.0, 1.0, 1.0);
              //color = vec3(auxUv.x, auxUv.y, 0.5);

              gl_FragColor = vec4(color, 1.0);
          }
        </script>

        <script id="shader-tex-fs" type="x-shader/x-fragment">
            precision highp float;

            varying vec3 vNormal;
            varying vec3 vPosWorld;
            varying vec2 vUv;
            varying vec3 modelPosition;

            uniform sampler2D uSampler; // textura del objeto

            uniform vec3 cameraPosition;

            vec3 calculateIlumination(vec3 normal){
              const float ambientReflection = 0.3;
              const vec3 ambientLight = vec3(1.0, 1.0, 1.0);

              const vec3 lightVector = vec3(0, 1.0, 0);
              const float diffuseReflection = 0.05;
              const vec3 diffuseLight = vec3(1.0, 1.0, 1.0);

              const vec3 lightVector2 = normalize(vec3(1.0, 0.2, 0));
              const float diffuseReflection2 = 0.18;
              const vec3 diffuseLight2 = vec3(1.0, 1.0, 1.0);

              return ambientReflection * ambientLight + diffuseReflection * dot(lightVector, normal) * diffuseLight + diffuseReflection2 * dot(lightVector2, normal) * diffuseLight2;
            }

            void main(void) {

                vec2 auxUv = vUv;

                vec3 ilum = calculateIlumination(vNormal);

                float uvFactorX = 0.08;
                float uvFactorY = 0.08;
                float shiftY = 0.45;
                float shiftX = 0.55;

                auxUv.x = modelPosition.x * uvFactorX + shiftX;
                auxUv.y = modelPosition.y * uvFactorY + shiftY;

                vec3 texture=texture2D(uSampler, auxUv).xyz;
                vec3 color = mix(texture, ilum, 0.3);
                //color = vec3(vPosWorld.y - 100.0, 1.0, 1.0);
                //color = vec3(auxUv.x, auxUv.y, 0.0);

                gl_FragColor = vec4(color, 1.0);
                //gl_FragColor = vec4(vNormal, 1.0);
            }
          </script>

          <script id="shader-tex-2-fs" type="x-shader/x-fragment">
              precision highp float;
  
              varying vec3 vNormal;
              varying vec3 vPosWorld;
              varying vec2 vUv;
              varying vec3 modelPosition;
  
              uniform sampler2D uSampler; // textura del objeto
  
              void main(void) {
  
                  vec2 auxUv = vUv;
  
                  vec3 lightVec=normalize(vec3(0.0,3.0,5.0)-vPosWorld);
                  vec3 diffColor=mix(vec3(0.7,0.7,0.7),vNormal,0.4);
                  vec3 color=dot(lightVec,vNormal)*diffColor+vec3(0.2,0.2,0.2);
  
                  float uvFactorX = 0.25;
                  float uvFactorY = 0.5;
                  float shiftX = -0.5;
                  float shiftY = 0.0;
                  
                  auxUv.x = modelPosition.x * uvFactorX + shiftX;
                  auxUv.y = modelPosition.z * uvFactorY + shiftY;
  
                  vec3 texture=texture2D(uSampler, auxUv).xyz;
                  
                  color = mix(texture, color, 0.05);
                  
                  //color = vec3(vPosWorld.y - 100.0, 1.0, 1.0);
                  //color = vec3(auxUv.x, auxUv.y, 0.0);
  
                  gl_FragColor = vec4(color, 1.0);
              }
      </script>

        <script id="terrain-fs" type="x-shader/x-fragment">
            precision highp float;
            varying vec3 vNormal;
            varying vec3 vPosWorld;
            varying float height;
            uniform vec3 lightVector;
            varying vec2 vUv;

            uniform sampler2D uSamplerH;
            uniform sampler2D uSampler0;    //texturas del mapa
            uniform sampler2D uSampler1;
            uniform sampler2D uSampler2;

            uniform vec3 cameraPosition;

            // Perlin Noise						
                  
            vec3 mod289(vec3 x)
            {
              return x - floor(x * (1.0 / 289.0)) * 289.0;
            }

            vec4 mod289(vec4 x)
            {
              return x - floor(x * (1.0 / 289.0)) * 289.0;
            }

            vec4 permute(vec4 x)
            {
              return mod289(((x*34.0)+1.0)*x);
            }

            vec4 taylorInvSqrt(vec4 r)
            {
              return 1.79284291400159 - 0.85373472095314 * r;
            }

            vec3 fade(vec3 t) {
              return t*t*t*(t*(t*6.0-15.0)+10.0);
            }

            // Classic Perlin noise
            float cnoise(vec3 P)
            {
              vec3 Pi0 = floor(P); // Integer part for indexing
              vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
              Pi0 = mod289(Pi0);
              Pi1 = mod289(Pi1);
              vec3 Pf0 = fract(P); // Fractional part for interpolation
              vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
              vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
              vec4 iy = vec4(Pi0.yy, Pi1.yy);
              vec4 iz0 = Pi0.zzzz;
              vec4 iz1 = Pi1.zzzz;

              vec4 ixy = permute(permute(ix) + iy);
              vec4 ixy0 = permute(ixy + iz0);
              vec4 ixy1 = permute(ixy + iz1);

              vec4 gx0 = ixy0 * (1.0 / 7.0);
              vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
              gx0 = fract(gx0);
              vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
              vec4 sz0 = step(gz0, vec4(0.0));
              gx0 -= sz0 * (step(0.0, gx0) - 0.5);
              gy0 -= sz0 * (step(0.0, gy0) - 0.5);

              vec4 gx1 = ixy1 * (1.0 / 7.0);
              vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
              gx1 = fract(gx1);
              vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
              vec4 sz1 = step(gz1, vec4(0.0));
              gx1 -= sz1 * (step(0.0, gx1) - 0.5);
              gy1 -= sz1 * (step(0.0, gy1) - 0.5);

              vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
              vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
              vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
              vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
              vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
              vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
              vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
              vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

              vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
              g000 *= norm0.x;
              g010 *= norm0.y;
              g100 *= norm0.z;
              g110 *= norm0.w;
              vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
              g001 *= norm1.x;
              g011 *= norm1.y;
              g101 *= norm1.z;
              g111 *= norm1.w;

              float n000 = dot(g000, Pf0);
              float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
              float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
              float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
              float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
              float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
              float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
              float n111 = dot(g111, Pf1);

              vec3 fade_xyz = fade(Pf0);
              vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
              vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
              float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
              return 2.2 * n_xyz;
            }

            vec3 calculateIlumination(vec3 ambientColor, vec3 normal, vec3 lightVector1, vec3 lightVector2){
              ambientColor = ambientColor * 0.8;
              const float ambientReflection = 0.7;
              const vec3 ambientLight = vec3(1.0, 1.0, 1.0);

              const float diffuseReflection1 = 0.2;
              const vec3 diffuseLight1 = vec3(1.0, 1.0, 1.0);

              const float diffuseReflection2 = 0.2;
              const vec3 diffuseLight2 = vec3(1.0, 1.0, 1.0);

              const float specularShiny = 5.0;
              const float specularReflection = 0.4;
              const vec3 specularLight = vec3(1.0, 1.0, 1.0);

              float lambertian = max(dot(normal, lightVector1), 0.0);
              float specular = 0.0;
              if(lambertian > 0.0) {
                vec3 R = reflect(-lightVector1, normal);
                vec3 V = normalize(vPosWorld - cameraPosition);
                float specAngle = max(dot(R, V), 0.0);
                specular = pow(specAngle, specularShiny);
              }

              return ambientReflection * ambientColor + diffuseReflection1 * max(dot(normal, lightVector1), 0.0) * diffuseLight1 + diffuseReflection2 * max(dot(normal, lightVector2), 0.0) * diffuseLight2 + specular * specularReflection * specularLight;
            }

            void main(void) {
              float low = -0.5;
              float high = 0.5;
              float scale1 = 1.0;
              float minH = 65.0;
              float h = vPosWorld.z - minH;
              float h1 = 0.0;
              float h2 = 50.0;
              float h3 = 100.0;

              vec3 pasto1=texture2D(uSampler2,vUv*4.0*scale1).xyz;
              vec3 pasto2=texture2D(uSampler2,vUv*3.77*scale1).xyz;
              vec3 pasto3=texture2D(uSampler2,vUv*2.11*scale1).xyz;
              
              // sampleo la tierra a diferentes escalas

              vec3 tierra1=texture2D(uSampler0,vUv*4.0*scale1).xyz;
              vec3 tierra2=texture2D(uSampler0,vUv*2.77*scale1).xyz;
              
              // sampleo la roca
              vec3 roca=texture2D(uSampler1,vUv*2.77*scale1).xyz;
              
              // combino los 3 sampleos del pasto con la funcion de mezcla
              vec3 color1=mix(mix(pasto1,pasto2,0.5),pasto3,0.3);
              
              // genero una mascara 1 a partir de ruido perlin
              float noise1=cnoise(vUv.xyx*8.23*scale1+23.11);
              float noise2=cnoise(vUv.xyx*11.77*scale1+9.45);
              float noise3=cnoise(vUv.xyx*14.8*scale1+21.2);
              
              float mask1=mix(mix(noise1,noise2,0.5),noise3,0.3);		
              mask1=smoothstep(-0.1,0.2,mask1);
              mask1 = mask1 * smoothstep(h1, h2, h);
              
              // combino tierra y roca usando la mascara 1
              vec3 color2=mix(mix(tierra1,tierra2,0.5),roca,mask1);
              
              // genero la mascara 2 a partir del ruido perlin
              float noise4=cnoise(vUv.xyx*8.23*scale1);
              float noise5=cnoise(vUv.xyx*11.77*scale1);
              float noise6=cnoise(vUv.xyx*14.8*scale1);
              
              float mask2=mix(mix(noise4,noise5,0.5),noise6,0.3);			   
              mask2=smoothstep(low,high,mask2);
              mask2 = mask2 * smoothstep(h2, h3, h);
              
              // combino color1 (tierra y rocas) con color2 a partir de la mascara2
              vec3 color=mix(color1,color2,mask2);
              const vec3 lightVector1 = vec3(0, 1.0, 0);
              const vec3 lightVector2 = vec3(1.0, 1.0, 0);
              //lightVector2 = normalize(lightVector2);
              color = calculateIlumination(color, vNormal, lightVector1, lightVector2);   
              
              gl_FragColor = vec4(color,1.0);
              //gl_FragColor = vec4(vNormal, 1.0);
              //gl_FragColor = vec4(1.0, 1.0, 1.0,1.0);
            }
        </script>

        <script id="water-fs" type="x-shader/x-fragment">
          precision highp float;
          varying vec3 vNormal;
          varying vec3 vPosWorld;
          varying float height;
          varying vec2 vUv;

          uniform vec3 lightVector;
          uniform sampler2D uSampler1;
          uniform sampler2D uSampler2;

          uniform vec3 cameraPos;
          
          void main(void) {
              vec3 worldNormal = vec3(0, 1.0, 0);
              vec3 eyeToSurfaceDir = normalize(vPosWorld - cameraPos);
              //eyeToSurfaceDir = normalize(cameraPos);
              vec3 direction = reflect(eyeToSurfaceDir, vNormal);
              vec2 directionUV = normalize(direction.xz);

              vec3 texture1=texture2D(uSampler1, vUv).xyz;
              vec3 texture2=texture2D(uSampler2, directionUV).xyz;

              vec3 color;

              color = mix(texture1, texture2, 0.4);
              //color = eyeToSurfaceDir;

              gl_FragColor = vec4(color, 1.0);
          }
      </script>


    </body>
</html>
